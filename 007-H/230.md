xiaoming90

high

# Malicious liquidators could inject stale symbol pricing during the liquidation process of PartyA

## Summary

Malicious liquidators could inject stale symbol pricing during the liquidation process to give themselves some benefits, leading to the loss of assets to the victim party.

## Vulnerability Detail

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L50

```solidity
File: LibMuon.sol
50:     function verifyPrices(PriceSig memory priceSig, address partyA) internal view {
51:         MuonStorage.Layout storage muonLayout = MuonStorage.layout();
52:         require(priceSig.prices.length == priceSig.symbolIds.length, "LibMuon: Invalid length");
53:         bytes32 hash = keccak256(
54:             abi.encodePacked(
55:                 muonLayout.muonAppId,
56:                 priceSig.reqId,
57:                 address(this),
58:                 partyA,
59:                 priceSig.upnl,
60:                 priceSig.totalUnrealizedLoss,
61:                 priceSig.symbolIds,
62:                 priceSig.prices,
63:                 priceSig.timestamp,
64:                 getChainId()
65:             )
66:         );
67:         verifyTSSAndGateway(hash, priceSig.sigs, priceSig.gatewaySignature);
68:     }
```

For most signature verification methods, such as `verifyPartyAUpnl` or `verifyPartyAUpnlAndPrice`, it imposes a [`muonLayout.upnlValidTime`](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L90) timeout. If the signature exceeds the `muonLayout.upnlValidTime` after Muon generates the signature, the signature will be rejected, and the verification will fail.

However, the `verifyPrices` function does not implement any signature expiry mechanism. As long as the signature is valid, passing the signature to the `verifyPrices` function at any time will always pass the verification. Another note is that there is also no nonce within the `verifyPrice`. Therefore, the user's action will not invalid the price signature generated earlier.

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L34

```solidity
File: LiquidationFacetImpl.sol
34:     function setSymbolsPrice(address partyA, PriceSig memory priceSig) internal {
35:         MAStorage.Layout storage maLayout = MAStorage.layout();
36:         AccountStorage.Layout storage accountLayout = AccountStorage.layout();
37: 
38:         LibMuon.verifyPrices(priceSig, partyA);
39:         require(maLayout.liquidationStatus[partyA], "LiquidationFacet: PartyA is solvent");
40:         require(
41:             priceSig.timestamp <=
42:                 maLayout.liquidationTimestamp[partyA] + maLayout.liquidationTimeout,
43:             "LiquidationFacet: Expired signature"
44:         );
```

In Line 41 of the `setSymbolsPrice`, it was observed that the function accepts the price signature as long it does not exceed `maLayout.liquidationTimestamp[partyA] + maLayout.liquidationTimeout`, which is the liquidation timestamp + 10 minutes. In short, as long as the price signatures are generated before that timestamp, they will be accepted. 

For instance, the liquidation timestamp is set to the current timestamp. In this case, the following price signatures are considered acceptable:

- Price signatures generated at the future - current timestamp + 5 minutes, current timestamp + 9 minutes
- Price signatures generated 1 hour, 1 day, 1 week, or 1 year ago will be accepted.

The following price signatures will be not be accepted:

- Price signature generated at the future - current timestamp + 11 minutes, current timestamp + 1 day

Malicious liquidators could exploit these weaknesses to set symbol prices to their advantage. On the other hand, PartyA and PartyB could also exploit this to swing the symbol prices in their favor during the liquidation since `setSymbolsPrice` function if they conspire with the liquidators.

Malicious liquidators could generate a new price signature from Muon every time interval  (e.g. every day, every week) and keep a collection of past price signatures. When it is time for them to carry out the liquidation against an account, they could cherry-pick the best price signature from their collection to give them the most benefits.

## Impact

When the liquidation process is not fair, it will lead to a loss of assets. For instance:

- If the PartyA's loss is higher than the liquidation fee in PartyA account, the liquidator will not receive any liquidation fee. Thus, when liquidating PartyA, the liquidator could try to minimize the loss of PartyA to maximize their return. In this case, loss of assets for PartyB as they received less assets than expected from the liquidation, but PartyA and liquidators benefits from this.
- PartyB could collaborate with liquidators to set a higher price so that PartyB will receive more assets than expected from PartyA during the liquidation. Loss of assets for PartyA.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L34

## Tool used

Manual Review

## Recommendation

Consider implementing the following validation check to ensure that the price signature older than the `maLayout.liquidationTimeout` (10 minutes) is rejected. This effectively prevents stale symbol pricing from injecting into the system.

```diff
function setSymbolsPrice(address partyA, PriceSig memory priceSig) internal {
    MAStorage.Layout storage maLayout = MAStorage.layout();
    AccountStorage.Layout storage accountLayout = AccountStorage.layout();

    LibMuon.verifyPrices(priceSig, partyA);
    require(maLayout.liquidationStatus[partyA], "LiquidationFacet: PartyA is solvent");
    require(
        priceSig.timestamp <=
            maLayout.liquidationTimestamp[partyA] + maLayout.liquidationTimeout,
        "LiquidationFacet: Expired signature"
    );
+    require(
+        block.timestamp <= priceSig.timestamp + maLayout.liquidationTimeout,
+        "LiquidationFacet: Expired price sig"
+    );
```

The above validation check has already been implemented within the [`liquidatePositionsPartyB`](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L327) function, but this was not consistently applied to PartyA's `setSymbolsPrice` function.