berndartmueller

high

# Expired signatures with outdated prices can be used to liquidate Party A's positions

## Summary

The `setSymbolsPrice` function and the `LibMuon.verifyPrices` function fail to validate if the provided signature for the symbol prices is expired, allowing the liquidator to use old, outdated, and possibly unfavorable prices to liquidate Party A's positions.

## Vulnerability Detail

The liquidation process for the insolvent Party A is executed in several steps. First, the liquidation status (`liquidationStatus`) and timestamp (`liquidationTimestamp`) of an insolvent Party A is set to true via the `liquidatePartyA` function.

Subsequently, the `setSymbolsPrice` function is called by the liquidator to initialize the symbol prices for Party A's positions as well as the `LiquidationDetail`. The prices are provided within the `PriceSig` struct as the `priceSig` parameter. Those values are retrieved from and signed by the Muon app (i.e., the Muon gateway address) to verify later its authenticity.

The validity of the data, i.e., the signature, is[ verified via the `LibMuon.verifyPrices` function](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L38). However, this function nor the `setSymbolsPrice` function verify if the signature is already expired. The only verification is that the [signature was created before the liquidation timeout](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L40-L44) (`maLayout.liquidationTimestamp[partyA] + maLayout.liquidationTimeout`) is reached.

This allows a liquidator to continuously gather Muon price data for Party A, and once Party A is insolvent, use those previously gathered, outdated prices to liquidate Party A's positions.

## Impact

Old and expired signatures for the `PriceSig` parameter can be used, resulting in liquidating Party A's position with outdated prices. This allows the liquidator to influence Party A's UPnL and thus affecting the available balance and the resulting liquidation type (`liquidationType`) and, ultimately, Party B's proceeds. Depending on the liquidation type, which is either set to `NORMAL`, `LATE`, `OVERDUE`, Party B has to bear the deficit caused by Party A's higher losses.

## Code Snippet

[contracts/facets/liquidation/LiquidationFacetImpl.sol#L40-L44](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L40-L44)

```solidity
34: function setSymbolsPrice(address partyA, PriceSig memory priceSig) internal {
35:     MAStorage.Layout storage maLayout = MAStorage.layout();
36:     AccountStorage.Layout storage accountLayout = AccountStorage.layout();
37:
38: @>  LibMuon.verifyPrices(priceSig, partyA); // @audit-issue does not verify if the signature is expired
39:     require(maLayout.liquidationStatus[partyA], "LiquidationFacet: PartyA is solvent");
40: @>  require(
41: @>      priceSig.timestamp <= // @audit-issue old and expired signatures with outdated prices can be used
42: @>          maLayout.liquidationTimestamp[partyA] + maLayout.liquidationTimeout,
43: @>      "LiquidationFacet: Expired signature"
44: @>  );
45:     for (uint256 index = 0; index < priceSig.symbolIds.length; index++) {
46:         accountLayout.symbolsPrices[partyA][priceSig.symbolIds[index]] = Price(
47:             priceSig.prices[index],
48:             maLayout.liquidationTimestamp[partyA]
49:         );
50:     }
51:
...     // [...]
97: }
```

[contracts/libraries/LibMuon.verifyPrices](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L50)

```solidity
50: function verifyPrices(PriceSig memory priceSig, address partyA) internal view {
51:     MuonStorage.Layout storage muonLayout = MuonStorage.layout();
52:     require(priceSig.prices.length == priceSig.symbolIds.length, "LibMuon: Invalid length");
53:     bytes32 hash = keccak256(
54:         abi.encodePacked(
55:             muonLayout.muonAppId,
56:             priceSig.reqId,
57:             address(this),
58:             partyA,
59:             priceSig.upnl,
60:             priceSig.totalUnrealizedLoss,
61:             priceSig.symbolIds,
62:             priceSig.prices,
63:             priceSig.timestamp,
64:             getChainId()
65:         )
66:     );
67:     verifyTSSAndGateway(hash, priceSig.sigs, priceSig.gatewaySignature);
68: }
```

## Tool used

Manual Review

## Recommendation

Consider checking if the provided signature is expired, similar to the [commented check in `LibMuon.verifyPartyAUpnl`](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L89-L92):

```solidity
require(
    block.timestamp <= upnlSig.timestamp + muonLayout.upnlValidTime,
    "LibMuon: Expired signature"
);
```
