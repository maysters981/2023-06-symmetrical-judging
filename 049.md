volodya

high

# Solvency is not being checked correctly on requestToClosePosition function

## Summary
Some changing position calls will not be reverted due to incorrect solvency check on requestToClosePosition
## Vulnerability Detail
Whevener partyA would like to close a position there is a call to `requestToClosePosition`. There is a check for solvency `LibSolvency.isSolventAfterRequestToClosePosition`
There is `unlockedAmount` being calculated in solvency while actually there is no unlocking happening in `requestToClosePosition` after checking solvency - no subtraction from `accountLayout.lockedBalances[quote.partyA]`

```solidity
    function isSolventAfterRequestToClosePosition(
        uint256 quoteId,
        uint256 closePrice,
        uint256 quantityToClose,
        SingleUpnlAndPriceSig memory upnlSig
    ) internal view returns (bool) {
        Quote storage quote = QuoteStorage.layout().quotes[quoteId];
        uint256 unlockedAmount = (quantityToClose *
            (quote.lockedValues.cva + quote.lockedValues.lf)) / LibQuote.quoteOpenAmount(quote);
....
```
[symmio-core/contracts/libraries/LibSolvency.sol#L164](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibSolvency.sol#L164)

```solidity
    function requestToClosePosition(
        uint256 quoteId,
        uint256 closePrice,
        uint256 quantityToClose,
        OrderType orderType,
        uint256 deadline,
        SingleUpnlAndPriceSig memory upnlSig
    ) internal {
...
        LibSolvency.isSolventAfterRequestToClosePosition(
            quoteId,
            closePrice,
            quantityToClose,
            upnlSig
        );

        // check that remaining position is not too small
        if (LibQuote.quoteOpenAmount(quote) > quantityToClose) {
            require(
                ((LibQuote.quoteOpenAmount(quote) - quantityToClose) * quote.lockedValues.total()) /
                    LibQuote.quoteOpenAmount(quote) >=
                    symbolLayout.symbols[quote.symbolId].minAcceptableQuoteValue,
                "PartyAFacet: Remaining quote value is low"
            );
        }

        accountLayout.partyANonces[quote.partyA] += 1;
        quote.modifyTimestamp = block.timestamp;
        quote.quoteStatus = QuoteStatus.CLOSE_PENDING;
        quote.requestedClosePrice = closePrice;
        quote.quantityToClose = quantityToClose;
        quote.orderType = orderType;
        quote.deadline = deadline;
    }

```
[/PartyA/PartyAFacetImpl.sol#L167](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol#L167)
## Impact
Positions might be liquidated right after calling requestToClosePosition
## Code Snippet

## Tool used

Manual Review

## Recommendation

```diff
   function isSolventAfterRequestToClosePosition(
        uint256 quoteId,
        uint256 closePrice,
        uint256 quantityToClose,
        SingleUpnlAndPriceSig memory upnlSig
    ) internal view returns (bool) {
        Quote storage quote = QuoteStorage.layout().quotes[quoteId];
-        uint256 unlockedAmount = (quantityToClose *
-            (quote.lockedValues.cva + quote.lockedValues.lf)) / LibQuote.quoteOpenAmount(quote);

        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            msg.sender
-        ) + int256(unlockedAmount);
+        );

        require(availableBalance >= 0, "LibSolvency: Available balance is lower than zero");
        if (quote.positionType == PositionType.LONG && closePrice <= upnlSig.price) {
            require(
                uint256(availableBalance) >=
                    ((quantityToClose * (upnlSig.price - closePrice)) / 1e18),
                "LibSolvency: partyA will be liquidatable"
            );
        } else if (quote.positionType == PositionType.SHORT && closePrice >= upnlSig.price) {
            require(
                uint256(availableBalance) >=
                    ((quantityToClose * (closePrice - upnlSig.price)) / 1e18),
                "LibSolvency: partyA will be liquidatable"
            );
        }
        return true;
    }
```

