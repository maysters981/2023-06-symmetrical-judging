berndartmueller

medium

# Party B can grief Party A by filling the close request for a limit order with a zero amount

## Summary

Party B can grief Party A by filling the close request with a zero amount via the `fillCloseRequest` function in the `PartyBFacetImpl` library. This allows Party B to maliciously invalidate Party A's provided signatures (provided by the Muon App) due to the increased nonces and prevent Party A from force closing a position due to the updated quote `modifyTimestamp`.

## Vulnerability Detail

Party B can fill Party A's close request with the `fillCloseRequest` function in the `PartyBFacetImpl` library. By specifying the amount to fill (`filledAmount`), Party B can either partially or entirely fill the quote (please note that a market order can not be partially filled).

However, by specifying a `filledAmount` of **zero**, the function will execute without reverting or actually filling the quote. Instead, the [nonces for both Party A and Party B are increased](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L290-L291) and the quote's `modifyTimestamp` is [updated to the current block timestamp](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L153).

Please note that the quote has to be already partially filled to some extent so that `quote.closedAmount` is greater than zero. Otherwise, the `LibQuote.closeQuote` function will [revert due to a division by zero error](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L183-L184).

## Impact

Party B being able to purposefully update the quote's `modifyTimestamp` to the current block timestamp without actually (partially) filling the quote can negatively impact the following functionality:

1. Invalidating the signature of the provided Muon App data due to the increased Party B's nonce, causing a revert. This affects the following functions:
   - `AccountFacetImpl.deallocate`
   - `LiquidationFacetImpl.liquidatePartyA`
   - `PartyAFacetImpl.sendQuote`
   - `PartyAFacetImpl.requestToClosePosition`
2. Griefing `PartyAFacetImpl.forceClosePosition` due to the extended cooldown period and the [`require` statement reverting ](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol#L260-L263)

## Code Snippet

[contracts/facets/PartyB/PartyBFacetImpl.fillCloseRequest](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L256)

```solidity
256: function fillCloseRequest(
257:     uint256 quoteId,
258:     uint256 filledAmount, // @audit-issue can be zero
259:     uint256 closedPrice,
260:     PairUpnlAndPriceSig memory upnlSig
261: ) internal {
262:     AccountStorage.Layout storage accountLayout = AccountStorage.layout();
263:     Quote storage quote = QuoteStorage.layout().quotes[quoteId];
264:     require(
265:         quote.quoteStatus == QuoteStatus.CLOSE_PENDING ||
266:         quote.quoteStatus == QuoteStatus.CANCEL_CLOSE_PENDING,
267:         "PartyBFacet: Invalid state"
268:     );
269:     require(block.timestamp <= quote.deadline, "PartyBFacet: Quote is expired");
270:     if (quote.positionType == PositionType.LONG) {
271:         require(
272:             closedPrice >= quote.requestedClosePrice,
273:             "PartyBFacet: Closed price isn't valid"
274:         );
275:     } else {
276:         require(
277:             closedPrice <= quote.requestedClosePrice,
278:             "PartyBFacet: Closed price isn't valid"
279:         );
280:     }
281:     if (quote.orderType == OrderType.LIMIT) {
282:         require(quote.quantityToClose >= filledAmount, "PartyBFacet: Invalid filledAmount");
283:     } else {
284:         require(quote.quantityToClose == filledAmount, "PartyBFacet: Invalid filledAmount");
285:     }
286:
287:     LibMuon.verifyPairUpnlAndPrice(upnlSig, quote.partyB, quote.partyA, quote.symbolId);
288:     LibSolvency.isSolventAfterClosePosition(quoteId, filledAmount, closedPrice, upnlSig);
289:
290:     accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
291:     accountLayout.partyANonces[quote.partyA] += 1;
292:     LibQuote.closeQuote(quote, filledAmount, closedPrice);
293: }
```

[contracts/libraries/LibQuote.sol#L153](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L153)

```solidity
149: function closeQuote(Quote storage quote, uint256 filledAmount, uint256 closedPrice) internal {
150:     QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
151:     AccountStorage.Layout storage accountLayout = AccountStorage.layout();
152:
153:     quote.modifyTimestamp = block.timestamp;
154:
...      // [...]
208: }
```

## Tool used

Manual Review

## Recommendation

Consider preventing filling a close request with the `fillCloseRequest` function with `filledAmount` of zero.
