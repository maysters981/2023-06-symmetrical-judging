0xmuxyz

medium

# A PartyB is able to lock a quote of the `quoteId` - even if that quote of the `quoteId` is already expired.

## Summary
A PartyB is supposed to be not able to lock a quote of the `quoteId`, which is already expired.

However, within the PartyBFacet#`lockQuote()` and the PartyBFacetImpl#`lockQuote()`, there is no validation to check whether or not a quote of the given `quoteId` would already be expired. 

This allow a PartyB to be able to lock a quote of the `quoteId` - even if that quote of the `quoteId` would be expired, which is unexpected-behavior of PartyB.

## Vulnerability Detail
When a PartyA send a quote, the PartyAFacet#`sendQuote()` would be called.
Within the PartyAFacet#`sendQuote()`, a expiration timestamp would be assigned into the `deadline` parameter.
And then, it would be assigned into the PartyAFacetImpl#`sendQuote()` like this:
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacet.sol#L24
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacet.sol#L38
```solidity
    function sendQuote(
        address[] memory partyBsWhiteList,
        uint256 symbolId,
        PositionType positionType,
        OrderType orderType,
        uint256 price,
        uint256 quantity,
        uint256 cva,
        uint256 mm,
        uint256 lf,
        uint256 maxInterestRate,
        uint256 deadline,  /// @audit
        SingleUpnlAndPriceSig memory upnlSig
    ) external whenNotPartyAActionsPaused notLiquidatedPartyA(msg.sender) notSuspended(msg.sender) {
        uint256 quoteId = PartyAFacetImpl.sendQuote(
            partyBsWhiteList,
            symbolId,
            positionType,
            orderType,
            price,
            quantity,
            cva,
            mm,
            lf,
            maxInterestRate,
            deadline,  /// @audit
            upnlSig
        );
        Quote storage quote = QuoteStorage.layout().quotes[quoteId];
```

Within the PartyAFacetImpl#`sendQuote()`, the `deadline`, which the expiration timestamp is stored, would be assigned to create a quote like this: 
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol#L32
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol#L113
```solidity
    function sendQuote(
        address[] memory partyBsWhiteList,
        uint256 symbolId,
        PositionType positionType,  
        OrderType orderType,       
        uint256 price,
        uint256 quantity, 
        uint256 cva,
        uint256 mm,
        uint256 lf,
        uint256 maxInterestRate,
        uint256 deadline,  /// @audit info - The user should set a deadline for their request. If no PartyB takes action on the quote within this timeframe, the request will expire.
        SingleUpnlAndPriceSig memory upnlSig
    ) internal returns (uint256 currentId) {
        ...
        require(deadline >= block.timestamp, "PartyAFacet: Low deadline");
        ...

        // create quote.
        Quote memory quote = Quote({
            id: currentId,
            partyBsWhiteList: partyBsWhiteList,
            symbolId: symbolId,
            positionType: positionType,
            orderType: orderType,
            openedPrice: 0,
            requestedOpenPrice: price,
            marketPrice: upnlSig.price,
            quantity: quantity,
            closedAmount: 0,
            lockedValues: lockedValues,
            initialLockedValues: lockedValues,
            maxInterestRate: maxInterestRate,
            partyA: msg.sender,
            partyB: address(0),
            quoteStatus: QuoteStatus.PENDING,
            avgClosedPrice: 0,
            requestedClosePrice: 0,
            parentId: 0,
            createTimestamp: block.timestamp,
            modifyTimestamp: block.timestamp,
            quantityToClose: 0,
            deadline: deadline /// @audit
        });
        quoteLayout.quoteIdsOf[msg.sender].push(currentId);
        quoteLayout.partyAPendingQuotes[msg.sender].push(currentId);
        quoteLayout.quotes[currentId] = quote;
```

Once a PartyB accept a quote sent, the PartyB call the PartyBFacet#`lockQuote()` to lock the quote of the `quoteId`.
Within the PartyBFacet#`lockQuote()`, the PartyBFacetImpl#`lockQuote()` would be called and the `quoteId` would be assigned into there like this:
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacet.sol#L21
```solidity
    function lockQuote(
        uint256 quoteId,
        SingleUpnlSig memory upnlSig
    ) external whenNotPartyBActionsPaused onlyPartyB notLiquidated(quoteId) {
        PartyBFacetImpl.lockQuote(quoteId, upnlSig, true); /// @audit
        Quote storage quote = QuoteStorage.layout().quotes[quoteId];
        ...
    }
```

Within the PartyBFacetImpl#`lockQuote()`, the quote of the `quoteId` would be locked by assigning the `QuoteStatus.LOCKED` into the `quote.quoteStatus` like this:
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L33
```solidity
    function lockQuote(uint256 quoteId, SingleUpnlSig memory upnlSig, bool increaseNonce) internal {
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();

        Quote storage quote = quoteLayout.quotes[quoteId];
        LibMuon.verifyPartyBUpnl(upnlSig, msg.sender, quote.partyA);
        checkPartyBValidationToLockQuote(quoteId, upnlSig.upnl);
        if (increaseNonce) {
            accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
        }
        quote.modifyTimestamp = block.timestamp;
        quote.quoteStatus = QuoteStatus.LOCKED;  /// @audit
        quote.partyB = msg.sender;
        // lock funds for partyB
        accountLayout.partyBPendingLockedBalances[msg.sender][quote.partyA].addQuote(quote);
        quoteLayout.partyBPendingQuotes[msg.sender][quote.partyA].push(quote.id);
    }
```

According to the [description of the `"SendQuote"` in the README](https://github.com/sherlock-audit/2023-06-symmetrical-0xmuxyz/tree/main/symmio-core#sendquote), it would be described that if no PartyB takes action on the quote within this timeframe (until `deadline`), the request of this quote will be expired like this:

> _**`deadline`**: The user should set a deadline for their request. If no PartyB takes action on the quote within this timeframe, the request will expire_

However, within the PartyBFacet#`lockQuote()` and the PartyBFacetImpl#`lockQuote()` above, there is no validation to check whether or not a quote of the given `quoteId` would already be expired. 
This allow a PartyB to be able to lock a quote of the `quoteId` - even if that quote of the `quoteId` would be expired.

## Impact
This allow a PartyB to be able to lock a quote - even if that quote would be expired, which is unexpected-behavior of PartyB.

## Code Snippet
- https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacet.sol#L17-L24
- https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L22-L38

## Tool used
Manual Review

## Recommendation
Within the PartyBFacet#`lockQuote()`, consider adding a validation in order to check whether or not a quote of the `quoteId` is already expired like this:
```solidity
    function lockQuote(
        uint256 quoteId,
        SingleUpnlSig memory upnlSig
    ) external whenNotPartyBActionsPaused onlyPartyB notLiquidated(quoteId) {
        PartyBFacetImpl.lockQuote(quoteId, upnlSig, true); /// @audit
        Quote storage quote = QuoteStorage.layout().quotes[quoteId];
+       require(block.timestamp <= quote.deadline, "PartyBFacet: Quote is expired"); 
        ...
    }
```
