Ruhum

medium

# FeeCollector withdrawing funds can prevent pending quotes from being canceled

## Summary
When pending quotes are canceled, party A gets their trading fees back. If the FeeCollector has withdrawn the funds before that, there won't be enough funds left to repay party A causing the tx to revert

## Vulnerability Detail
The trading fee logic is specified in `LibQuote`:

```sol
    function getTradingFee(uint256 quoteId) internal view returns (uint256 fee) {
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
        Quote storage quote = quoteLayout.quotes[quoteId];
        Symbol storage symbol = SymbolStorage.layout().symbols[quote.symbolId];
        if (quote.orderType == OrderType.LIMIT) {
            fee =
                (LibQuote.quoteOpenAmount(quote) * quote.requestedOpenPrice * symbol.tradingFee) /
                1e36;
        } else {
            fee = (LibQuote.quoteOpenAmount(quote) * quote.marketPrice * symbol.tradingFee) / 1e36;
        }
    }

    function returnTradingFee(uint256 quoteId) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
        accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] += tradingFee;
        accountLayout.balances[GlobalAppStorage.layout().feeCollector] -= tradingFee;
    }

    function receiveTradingFee(uint256 quoteId) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
        accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] -= tradingFee;
        accountLayout.balances[GlobalAppStorage.layout().feeCollector] += tradingFee;
    }
```
The fee is taken from party A and sent to the fee collector's address. The fee collector can withdraw the funds through the AccountFacet.

When party A sends a quote, they pay the trading fee: https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol#L119

When the quote is canceled, party A gets their trading fee back: https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol#L136

If the fee collector has withdrawn all their funds in the meantime, the pending quote can't be canceled. The tx will revert because of an underflow.

## Impact
Can't cancel pending quotes if fee collector withdraws their funds prematurely.

## Code Snippet
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L122-L148

## Tool used

Manual Review

## Recommendation
The fees should only be paid out to the fee collector **after** the trade was completed. Before that, the funds should stay locked in the contract.