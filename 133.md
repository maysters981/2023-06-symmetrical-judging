Juntao

medium

# PartyB can lock quotes without available balance

## Summary

By reusing the same Muon signature, PartyB can lock quotes without available balance.

## Vulnerability Detail

PartyB calls [lockQuote(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L22-L38) method to lock a pending quote sent by PartyA:
```solidity
    function lockQuote(uint256 quoteId, SingleUpnlSig memory upnlSig, bool increaseNonce) internal {
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();


        Quote storage quote = quoteLayout.quotes[quoteId];
        LibMuon.verifyPartyBUpnl(upnlSig, msg.sender, quote.partyA);
        checkPartyBValidationToLockQuote(quoteId, upnlSig.upnl);
        if (increaseNonce) {
            accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
        }
        quote.modifyTimestamp = block.timestamp;
        quote.quoteStatus = QuoteStatus.LOCKED;
        quote.partyB = msg.sender;
        // lock funds for partyB
        accountLayout.partyBPendingLockedBalances[msg.sender][quote.partyA].addQuote(quote);
        quoteLayout.partyBPendingQuotes[msg.sender][quote.partyA].push(quote.id);
    }
```
This method will first verify `upnlSig` signature:
```solidity
        LibMuon.verifyPartyBUpnl(upnlSig, msg.sender, quote.partyA);
```
Then calls [checkPartyBValidationToLockQuote(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L75-L110) to check if PartyB is qualified to lock the quote:
```solidity
        checkPartyBValidationToLockQuote(quoteId, upnlSig.upnl);
```
`checkPartyBValidationToLockQuote` checks if PartyB has enough available balance for this quote based on `upnlSig.upnl` and revert if not:
```solidity
        int256 availableBalance = LibAccount.partyBAvailableForQuote(
            upnl,
            msg.sender,
            quote.partyA
        );
        require(availableBalance >= 0, "PartyBFacet: Available balance is lower than zero");
        require(
            uint256(availableBalance) >= quote.lockedValues.total(),
            "PartyBFacet: insufficient available balance"
        );
```
Then partyBNonces is updated and quote is locked:
```solidity
        if (increaseNonce) {
            accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
        }
        quote.modifyTimestamp = block.timestamp;
        quote.quoteStatus = QuoteStatus.LOCKED;
        quote.partyB = msg.sender;
        // lock funds for partyB
        accountLayout.partyBPendingLockedBalances[msg.sender][quote.partyA].addQuote(quote);
        quoteLayout.partyBPendingQuotes[msg.sender][quote.partyA].push(quote.id);
```
The problem is that `partyBNonces` is updated before `quote.partyB` is set, because `quote.partyB` is always **address(0)** in a pending quote, `partyBNonces[PartyB][PartyA]` is in fact not updated at all.

Let's assume:

1. PartyB locks a quote from PartyA and open a position
2. PartyB suffers a loss in the position, `upnl` is decreased and he does not has any available balance
3. PartyB tries to lock another quote from PartyA with the same signature, because `partyBNonces` is not changed, signature verification will pass and protocol wrongly assumes PartyB has the same available balance as before, so PartyB is qualified to lock the quote
4. PartyB successfully locks the quote even if his actual available balance is insufficient for the quote

## Impact

PartyB can lock quotes even if his actual available balance is not enough.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L29-L37

## Tool used

Manual Review

## Recommendation

As confirmed with sponsor, this is not by design and should be addressed and fixed, `partyBNonces` should be updated after `quote.partyB` is set:
```diff
-       if (increaseNonce) {
-           accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
-       }
        quote.modifyTimestamp = block.timestamp;
        quote.quoteStatus = QuoteStatus.LOCKED;
        quote.partyB = msg.sender;
+       if (increaseNonce) {
+           accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
+       }
        // lock funds for partyB
        accountLayout.partyBPendingLockedBalances[msg.sender][quote.partyA].addQuote(quote);
        quoteLayout.partyBPendingQuotes[msg.sender][quote.partyA].push(quote.id);
```