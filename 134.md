Juntao

medium

# PartyB may allocate far less balances than expected by calling depositAndAllocateForPartyB

## Summary

PartyB may allocate far less balances than expected by using depositAndAllocateForPartyB, due to lack of decimal conversion.

## Vulnerability Detail

PartyB can call [depositAndAllocateForPartyB](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L74-L82) method to deposit and allocate funds at the same time:
```solidity
    function depositAndAllocateForPartyB(
        uint256 amount,
        address partyA
    ) external whenNotPartyBActionsPaused onlyPartyB {
        AccountFacetImpl.depositForPartyB(amount);
        AccountFacetImpl.allocateForPartyB(amount, partyA, true);
        emit DepositForPartyB(msg.sender, amount);
        emit AllocateForPartyB(msg.sender, partyA, amount);
    }
```
However, the collateral token used in protocol may be in **6** decimals and PartyB's `allocatedBalances` is in **18** decimals, `depositAndAllocateForPartyB` allocates right after deposit, in this case very few funds will be added to PartyB's `allocatedBalances`.
For example, PartyB want to deposit and allocate 100 USDC (`amount`: 100e6), firstly, [AccountFacetImpl.depositForPartyB(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L108) will convert the deposit amount into 18 decimals and added to `balances`:
```solidity
    function depositForPartyB(uint256 amount) internal {
        IERC20(GlobalAppStorage.layout().collateral).safeTransferFrom(
            msg.sender,
            address(this),
            amount
        );
        uint256 amountWith18Decimals = (amount * 1e18) /
        (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
        AccountStorage.layout().balances[msg.sender] += amountWith18Decimals;
    }
```
Then [allocateForPartyB(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L119-L132) simply allocates the same `amount` (100e6) of `balances` to `allocatedBalances` without conversion, meaning only 10e-10 (0.0000000001) balance is allcoated, which is far less than expected:
```solidity
    function allocateForPartyB(uint256 amount, address partyA, bool increaseNonce) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();


        require(accountLayout.balances[msg.sender] >= amount, "PartyBFacet: Insufficient balance");
        require(
            !MAStorage.layout().partyBLiquidationStatus[msg.sender][partyA],
            "PartyBFacet: PartyB isn't solvent"
        );
        if (increaseNonce) {
            accountLayout.partyBNonces[msg.sender][partyA] += 1;
        }
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.partyBAllocatedBalances[msg.sender][partyA] += amount;
    }
```

## Impact

Very few balances can be allocated.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L74-L82

## Tool used

Manual Review

## Recommendation

```diff
    function depositAndAllocateForPartyB(
        uint256 amount,
        address partyA
    ) external whenNotPartyBActionsPaused onlyPartyB {
        AccountFacetImpl.depositForPartyB(amount);
+        uint256 amountWith18Decimals = (amount * 1e18) / (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
-        AccountFacetImpl.allocateForPartyB(amount, partyA, true);
+        AccountFacetImpl.allocateForPartyB(amountWith18Decimals, partyA, true);
        emit DepositForPartyB(msg.sender, amount);
        emit AllocateForPartyB(msg.sender, partyA, amount);
    }
```
