libratus

medium

# Fee increase can be sandwiched by users to extract value

## Summary
User can sandwich trading fee increase with open/cancel quote requests and extract value.

## Vulnerability Detail
Admin can change symbol's trading fee by calling `setSymbolTradingFee`.
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/control/ControlFacet.sol#L164


This parameter is used it two places:
1. When partyA sends a quote request to withdraw the fee from partyA's balance
2. When a quote request is being cancelled to return the fee to partyA

This can be seen in the below snippet:
```solidity
    function getTradingFee(uint256 quoteId) internal view returns (uint256 fee) {
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
        Quote storage quote = quoteLayout.quotes[quoteId];
        Symbol storage symbol = SymbolStorage.layout().symbols[quote.symbolId];
        if (quote.orderType == OrderType.LIMIT) {
            fee =
                (LibQuote.quoteOpenAmount(quote) * quote.requestedOpenPrice * symbol.tradingFee) /
                1e36;
        } else {
            fee = (LibQuote.quoteOpenAmount(quote) * quote.marketPrice * symbol.tradingFee) / 1e36;
        }
    }

    function returnTradingFee(uint256 quoteId) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
        accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] += tradingFee;
        accountLayout.balances[GlobalAppStorage.layout().feeCollector] -= tradingFee;
    }

    function receiveTradingFee(uint256 quoteId) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
        accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] -= tradingFee;
        accountLayout.balances[GlobalAppStorage.layout().feeCollector] += tradingFee;
    }
```

If fee is increased inbetween these two actions, partyA will gain extra funds. It was confirmed by the sponsor that this functionality is relevant for existing symbols that are being traded. Therefore the exploit can be the following:
- Admin wants to increase the trading fee on one of the assets and sends `setSymbolTradingFee` transaction.
- User front-runs the transaction and opens a quote thus executing `receiveTradingFee` part of the code.
- After admin's transaction is executed, user cancels the quote triggering `returnTradingFee`.
- User receives more that was withheld on step 2.

## Impact
Attacker can extract value from the protocol when fee is increased on the active trading pair.

## Code Snippet
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/control/ControlFacet.sol#L164
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L122-L147
## Tool used

Manual Review

## Recommendation

Fee can be calculated and stored with the quote when it's first being sent:
```diff
diff --git a/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol b/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol
index 899a78d..dac8024 100644
--- a/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol
+++ b/symmio-core/contracts/facets/PartyA/PartyAFacetImpl.sol
@@ -110,7 +110,8 @@ library PartyAFacetImpl {
             createTimestamp: block.timestamp,
             modifyTimestamp: block.timestamp,
             quantityToClose: 0,
-            deadline: deadline
+            deadline: deadline,
+            tradingFee: LibQuote.getTradingFee(currentId)
         });
         quoteLayout.quoteIdsOf[msg.sender].push(currentId);
         quoteLayout.partyAPendingQuotes[msg.sender].push(currentId);
```

This way, when refunding the fee we will return the original value and not the updated.
```diff
diff --git a/symmio-core/contracts/libraries/LibQuote.sol b/symmio-core/contracts/libraries/LibQuote.sol
index fc43b58..3d55a62 100644
--- a/symmio-core/contracts/libraries/LibQuote.sol
+++ b/symmio-core/contracts/libraries/LibQuote.sol
@@ -141,7 +141,7 @@ library LibQuote {
 
     function receiveTradingFee(uint256 quoteId) internal {
         AccountStorage.Layout storage accountLayout = AccountStorage.layout();
-        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
+        uint256 tradingFee = QuoteStorage.layout().quotes[quoteId].tradingFee;
         accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] -= tradingFee;
         accountLayout.balances[GlobalAppStorage.layout().feeCollector] += tradingFee;
     }
```

Additional logic needs to be added to handle partially filled quotes