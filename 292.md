berndartmueller

medium

# Inaccurate solvency check during position opening can lead to immediate insolvency

## Summary

The `LibSolvency.isSolventAfterOpenPosition` solvency check when opening a position fails to take into account the potential adjustment of the quote's locked values if the `openedPrice` differs from the quote's `requestedOpenPrice`.
This adjustment is necessary to make sure the leverage is fixed at the initial value when Party A created the quote.

Incorrectly determining the solvency of both parties can lead to the immediate insolvency of either party after opening such a position.

## Vulnerability Detail

Opening a position by Party B adjusts the quote's locked values. This maintains the leverage initially set by Party A when creating the quote. To archive this, the quote's locked values are first multiplied with the open price (`openedPrice`) and then divided by Party A's requested open price (`requestedOpenPrice`), as seen for both full and partial openings in lines 163 and 184-185.

A higher `openedPrice` than the `requestedOpenPrice` leads to an inflation of the quote's locked values, whereas a lower `openedPrice` leads to a deflation of the quote's locked values.

However, the solvency check in `LibSolvency.isSolventAfterOpenPosition` does not incorporate this adjustment. Instead, the function [deducts the quote's locked values](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibSolvency.sol#L33-L44) (in proportion of the filled amount) from the available balances of Party A and B without considering the difference of the actual `openedPrice` and the initial `requestedOpenPrice` of the quote.

When opening a limit order with `openedPrice` being higher than the `requestedOpenPrice`, the quote's locked value will get inflated in the `PartyBFacetImpl.openPosition` function, whereas the solvency check will use the non-corrected values and thus underestimates the locked amount of the quote leading to a higher available balance. Consequently, the party is incorrectly considered solvent.

## Impact

Party A and Party B's solvency is affected, and opening a position with increased locked values can lead to immediate insolvency, thus putting the party at risk of getting liquidated.

## Code Snippet

[contracts/facets/PartyB/PartyBFacetImpl.sol#L150](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L150)

```solidity
112: function openPosition(
113:     uint256 quoteId,
114:     uint256 filledAmount,
115:     uint256 openedPrice,
116:     PairUpnlAndPriceSig memory upnlSig
117: ) internal returns (uint256 currentId) {
...      // [...]
148:
149:     quote.openedPrice = openedPrice;
150: @>  LibSolvency.isSolventAfterOpenPosition(quoteId, filledAmount, upnlSig);
151:
152:     accountLayout.partyANonces[quote.partyA] += 1;
153:     accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
154:     quote.modifyTimestamp = block.timestamp;
155:
156:     LibQuote.removeFromPendingQuotes(quote);
157:
158:     if (quote.quantity == filledAmount) {
159:         accountLayout.pendingLockedBalances[quote.partyA].subQuote(quote);
160:         accountLayout.partyBPendingLockedBalances[quote.partyB][quote.partyA].subQuote(quote);
161:
162:         if (quote.orderType == OrderType.LIMIT) {
163: @>          quote.lockedValues.mul(openedPrice).div(quote.requestedOpenPrice); // @audit-info Adjusts quote's locked values to fixate leverage
164:         }
165:         accountLayout.lockedBalances[quote.partyA].addQuote(quote);
166:         accountLayout.partyBLockedBalances[quote.partyB][quote.partyA].addQuote(quote);
167:     }
168:     // partially fill
169:     else {
170:         currentId = ++quoteLayout.lastId;
171:         QuoteStatus newStatus;
172:         if (quote.quoteStatus == QuoteStatus.CANCEL_PENDING) {
173:             newStatus = QuoteStatus.CANCELED;
174:         } else {
175:             newStatus = QuoteStatus.PENDING;
176:             quoteLayout.partyAPendingQuotes[quote.partyA].push(currentId);
177:         }
178:         LockedValues memory filledLockedValues = LockedValues(
179:             (quote.lockedValues.cva * filledAmount) / quote.quantity,
180:             (quote.lockedValues.mm * filledAmount) / quote.quantity,
181:             (quote.lockedValues.lf * filledAmount) / quote.quantity
182:         );
183:         LockedValues memory appliedFilledLockedValues = filledLockedValues;
184: @>      appliedFilledLockedValues = appliedFilledLockedValues.mulMem(openedPrice); // @audit-info Adjusts quote's locked values to fixate leverage
185: @>      appliedFilledLockedValues = appliedFilledLockedValues.divMem(quote.requestedOpenPrice);
...      // [...]
254: }
```

[contracts/libraries/LibSolvency.sol#L33-L44](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibSolvency.sol#L33-L44)

```solidity
15: function isSolventAfterOpenPosition(
16:     uint256 quoteId,
17:     uint256 filledAmount,
18:     PairUpnlAndPriceSig memory upnlSig
19: ) internal view returns (bool) {
20:     Quote storage quote = QuoteStorage.layout().quotes[quoteId];
21:     int256 partyBAvailableBalance = LibAccount.partyBAvailableBalance(
22:         upnlSig.upnlPartyB,
23:         quote.partyB,
24:         quote.partyA
25:     );
26:     int256 partyAAvailableBalance = LibAccount.partyAAvailableBalance(
27:         upnlSig.upnlPartyA,
28:         quote.partyA
29:     );
30:
31:     uint256 lockedAmount;
32:     uint256 lockedMM;
33: @>  if (quote.orderType == OrderType.LIMIT) {
34: @>      lockedAmount =
35: @>          (filledAmount * (quote.lockedValues.cva + quote.lockedValues.lf)) /
36: @>          quote.quantity;
37: @>      lockedMM = (filledAmount * quote.lockedValues.mm) / quote.quantity;
38: @>  } else {
39: @>      lockedAmount = quote.lockedValues.cva + quote.lockedValues.lf;
40: @>      lockedMM = quote.lockedValues.mm;
41: @>  }
42:
43: @>  partyAAvailableBalance -= int256(lockedAmount);
44: @>  partyBAvailableBalance -= int256(lockedAmount);
45:
46:     if (quote.positionType == PositionType.LONG) {
...     // [...]
```

## Tool used

Manual Review

## Recommendation

Consider incorporating the inflation/deflation of the quote's locked amount in the solvency check by providing `quote.requestedOpenPrice` to the `LibSolvency.isSolventAfterOpenPosition` function and incorporate it in the calculation of the locked amount.
