p0wd3r

high

# PartyA/B can avoid being liquidated through front-running allocate.

## Summary
PartyA/B can avoid being liquidated through front-running `allocate`.
## Vulnerability Detail
Both PartyA and PartyB have similar issues, but here we will only explain through the perspective of PartyA.

Through `allocate`, PartyA can update its `partyANonces`. The cost of this operation is very low, and the amount can even be 0.

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L41-L52
```solidity
    function allocate(uint256 amount) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        require(
            accountLayout.allocatedBalances[msg.sender] + amount <=
            GlobalAppStorage.layout().balanceLimitPerUser,
            "AccountFacet: Allocated balance limit reached"
        );
        require(accountLayout.balances[msg.sender] >= amount, "AccountFacet: Insufficient balance");
        accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
    }
```

In `liquidatePartyA`, there will be a check on `upnlSig`, and the result of the check depends on `partyANonces`.

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32
```solidity
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
        MAStorage.Layout storage maLayout = MAStorage.layout();

        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
        ....
    }
```

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L87-L106
```solidity
    function verifyPartyAUpnl(SingleUpnlSig memory upnlSig, address partyA) internal view {
        MuonStorage.Layout storage muonLayout = MuonStorage.layout();
//        require(
//            block.timestamp <= upnlSig.timestamp + muonLayout.upnlValidTime,
//            "LibMuon: Expired signature"
//        );
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                AccountStorage.layout().partyANonces[partyA],
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
        verifyTSSAndGateway(hash, upnlSig.sigs, upnlSig.gatewaySignature);
    }
```

This leads to PartyA being able to change `partyANonce` through front-running `allocate` before verifying the signature, causing the signature verification to fail.

The attack steps are as follows:
1. Liquidator calls `liquidatePartyA`, using `partyANonces` as 1 in the signature.
2. PartyA detected the call to `liquidatePartyA` in the mempool, and front-run `allocate`, causing `partyANonce` to become 2.
3. The signature verification failed during the validation of `liquidatePartyA` due to a change in `partyANonce`. The liquidation cannot proceed.
## Impact
PartyA/B can avoid being liquidated
## Code Snippet
- https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L41-L52
- https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32
- https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L87-L106
## Tool used

Manual Review

## Recommendation 
In the allocate function, check the liquidity of PartyA. The current modifier `notLiquidatedPartyA` is set in the `liquidatePartyA` function and does not solve this problem. Or you can add a timestamp to `partyANonce`.

Equally applicable to PartyB.