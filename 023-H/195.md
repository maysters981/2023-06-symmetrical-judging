Jiamin

high

# Liquidation will fail if user changes nonces

## Summary

Liquidation will fail if user changes nonces.

## Vulnerability Detail

When a user's positions are not supported by available balances (Locked MMs + Free Balance + Pending Locked), user will be liquidated.

The liquidator should first liquidate the user through function [liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32) and user will be marked as liquidated :
```solidity
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
        MAStorage.Layout storage maLayout = MAStorage.layout();


        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
        require(availableBalance < 0, "LiquidationFacet: PartyA is solvent");
        maLayout.liquidationStatus[partyA] = true;
        maLayout.liquidationTimestamp[partyA] = upnlSig.timestamp;
        AccountStorage.layout().liquidators[partyA].push(msg.sender);
    }
```
Parameter **upnlSig** is the Muon signature for user upnl and symbol price, it will be verified through function [verifyPartyAUpnl](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L87-L106) in **LibMuon.sol**:
```solidity
    function verifyPartyAUpnl(SingleUpnlSig memory upnlSig, address partyA) internal view {
        MuonStorage.Layout storage muonLayout = MuonStorage.layout();
//        require(
//            block.timestamp <= upnlSig.timestamp + muonLayout.upnlValidTime,
//            "LibMuon: Expired signature"
//        );
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                AccountStorage.layout().partyANonces[partyA],
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
        verifyTSSAndGateway(hash, upnlSig.sigs, upnlSig.gatewaySignature);
    }
```
The Muon signature is verified against the generated hash of the provided parameters, one of which is [partyANonces](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/storages/AccountStorage.sol#L43) defined in **AccountStorage.sol**.

The problem is that **partyANonces** can be changed by user, which will lead to a failed signature verification and liquidation will be reverted. This could be done through function [allocate(uint256 amount)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L41-L52):
```solidity
    function allocate(uint256 amount) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        require(
            accountLayout.allocatedBalances[msg.sender] + amount <=
            GlobalAppStorage.layout().balanceLimitPerUser,
            "AccountFacet: Allocated balance limit reached"
        );
        require(accountLayout.balances[msg.sender] >= amount, "AccountFacet: Insufficient balance");
        accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
    }
```
It can be seen from above that whenever user calls function **allocate**, **partyANonces** will be increased by 1, as there is no minimum allocating requirement, so user can allocate 0 amount to change **partyANonces**, and prevent positions from being liquidated.

## Impact

Liquidation will fail and user can not be liquidated.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L41-L52

## Tool used

Manual Review

## Recommendation

One simple way to mitigate this issue is not to increase **partyANonces** in function **allocate**.