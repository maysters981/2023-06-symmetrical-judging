0xcrunch

high

# PartyA can never be liquidated by manipulating partyANonces

## Summary

PartyA can never be liquidated by manipulating partyANonces.

## Vulnerability Detail

On the contract side, the liquidation of partyA is a four-step process. The liquidator should first liquidate the user:
```solidity
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
        MAStorage.Layout storage maLayout = MAStorage.layout();


        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
        require(availableBalance < 0, "LiquidationFacet: PartyA is solvent");
        maLayout.liquidationStatus[partyA] = true;
        maLayout.liquidationTimestamp[partyA] = upnlSig.timestamp;
        AccountStorage.layout().liquidators[partyA].push(msg.sender);
    }
```
SingleUpnlSig will be verified in LibMuon:
```solidity
        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
```
```solidity
    function verifyPartyAUpnl(SingleUpnlSig memory upnlSig, address partyA) internal view {
        MuonStorage.Layout storage muonLayout = MuonStorage.layout();
//        require(
//            block.timestamp <= upnlSig.timestamp + muonLayout.upnlValidTime,
//            "LibMuon: Expired signature"
//        );
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                AccountStorage.layout().partyANonces[partyA],
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
        verifyTSSAndGateway(hash, upnlSig.sigs, upnlSig.gatewaySignature);
    }
```
One of the parameters used in the hash for verification is partyANonces[partyA]:
```solidity
                AccountStorage.layout().partyANonces[partyA],
```
To pass verification, partyANonces[partyA] should be of the same value as it is in SingleUpnlSig. However, partyANonces[partyA] could be manipulated by PartyA through allocating:
```solidity
    function allocate(uint256 amount) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        require(
            accountLayout.allocatedBalances[msg.sender] + amount <=
            GlobalAppStorage.layout().balanceLimitPerUser,
            "AccountFacet: Allocated balance limit reached"
        );
        require(accountLayout.balances[msg.sender] >= amount, "AccountFacet: Insufficient balance");
        accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
    }
```
So if a PartyA front-runs liquidation and allocates, partyANonces[partyA] will be increased by 1, then the hash for verification becomes invalid and verification will fail. 

Please note PartyA can allocate 0 amount to increase partyANonces[partyA], which is basically cost-free.

## Impact

PartyA front-runs every liquidation and manipulates partyANonces[partyA] with no cost, thus can never be liquidated.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32

## Tool used

Manual Review

## Recommendation

Please consider to not increase partyANonces[partyA] when allocate:
```diff
-       accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
```
