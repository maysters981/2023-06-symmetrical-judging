kutugu

medium

# depositAndAllocateForPartyB method implementation error

## Summary

Function depositAndAllocateForPartyB implement error. The input amount of allocateForPartyB is not converted.

## Vulnerability Detail

Let's see how `depositAndAllocate` is implemented:
```solidity
    function depositAndAllocate(
        uint256 amount
    ) external whenNotAccountingPaused notLiquidatedPartyA(msg.sender) {
        AccountFacetImpl.deposit(msg.sender, amount);
        // @audit Calculate the internal rebase amount
        uint256 amountWith18Decimals = (amount * 1e18) /
        (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
        AccountFacetImpl.allocate(amountWith18Decimals);
        emit Deposit(msg.sender, msg.sender, amount);
        emit AllocatePartyA(msg.sender, amountWith18Decimals);
    }
```

But depositAndAllocateForPartyB does not implement the corresponding convertion:
```solidity
    function depositAndAllocateForPartyB(
        uint256 amount,
        address partyA
    ) external whenNotPartyBActionsPaused onlyPartyB {
        AccountFacetImpl.depositForPartyB(amount);
        AccountFacetImpl.allocateForPartyB(amount, partyA, true);
        emit DepositForPartyB(msg.sender, amount);
        emit AllocateForPartyB(msg.sender, partyA, amount);
    }

    function depositForPartyB(uint256 amount) internal {
        IERC20(GlobalAppStorage.layout().collateral).safeTransferFrom(
            msg.sender,
            address(this),
            amount
        );
        // @audit Calculate the internal rebase amount for depositForPartyB
        uint256 amountWith18Decimals = (amount * 1e18) /
        (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
        AccountStorage.layout().balances[msg.sender] += amountWith18Decimals;
    }

    function allocateForPartyB(uint256 amount, address partyA, bool increaseNonce) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();

        require(accountLayout.balances[msg.sender] >= amount, "PartyBFacet: Insufficient balance");
        require(
            !MAStorage.layout().partyBLiquidationStatus[msg.sender][partyA],
            "PartyBFacet: PartyB isn't solvent"
        );
        if (increaseNonce) {
            accountLayout.partyBNonces[msg.sender][partyA] += 1;
        }
        // @audit The amount operated here should be the amountWith18Decimals added by the depositForPartyB above, not the original input amount
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.partyBAllocatedBalances[msg.sender][partyA] += amount;
    }
```

## Impact

When the collateral decimals is far less than 1e18, amountWith18Decimals far greater than the amount user origin input. 
Lead to user balance can't completely allocate, is not in line with expectations.  

## Code Snippet

- https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L78-L79

## Tool used

Manual Review

## Recommendation

Convert the internal accounting balance before calling allocateForPartyB
```diff
diff --git a/symmio-core/contracts/facets/Account/AccountFacet.sol b/symmio-core/contracts/facets/Account/AccountFacet.sol
index 07a15cb..aa978b0 100644
--- a/symmio-core/contracts/facets/Account/AccountFacet.sol
+++ b/symmio-core/contracts/facets/Account/AccountFacet.sol
@@ -76,9 +76,11 @@ contract AccountFacet is Accessibility, Pausable, IAccountEvents {
         address partyA
     ) external whenNotPartyBActionsPaused onlyPartyB {
         AccountFacetImpl.depositForPartyB(amount);
-        AccountFacetImpl.allocateForPartyB(amount, partyA, true);
+        uint256 amountWith18Decimals = (amount * 1e18) /
+        (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
+        AccountFacetImpl.allocateForPartyB(amountWith18Decimals, partyA, true);
         emit DepositForPartyB(msg.sender, amount);
-        emit AllocateForPartyB(msg.sender, partyA, amount);
+        emit AllocateForPartyB(msg.sender, partyA, amountWith18Decimals);
     }
 
     function deallocateForPartyB(
```
