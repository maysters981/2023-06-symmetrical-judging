Juntao

high

# Attacker may drain funds from protocol when tradingFee is updated

## Summary

Attacker may drain funds from protocol, when trading fee rate is updated to a higher value.

## Vulnerability Detail

Protocol charges PartyA a trading fee when sends quote:
```solidity
    LibQuote.receiveTradingFee(currentId);
```
If the pending quote is canceled or expired, the trading fee will be returned:
```solidity
    LibQuote.returnTradingFee(quoteId);
```
The amount of both charged trading fee and returned trading fee is calculated in [getTradingFee(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L122C14-L133) method:
```solidity
    function getTradingFee(uint256 quoteId) internal view returns (uint256 fee) {
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
        Quote storage quote = quoteLayout.quotes[quoteId];
        Symbol storage symbol = SymbolStorage.layout().symbols[quote.symbolId];
        if (quote.orderType == OrderType.LIMIT) {
            fee =
                (LibQuote.quoteOpenAmount(quote) * quote.requestedOpenPrice * symbol.tradingFee) /
                1e36;
        } else {
            fee = (LibQuote.quoteOpenAmount(quote) * quote.marketPrice * symbol.tradingFee) / 1e36;
        }
    }
```
It can be seen the trading fee amount if base on symbol's `tradingFee` which is a global state, also this `tradingFee` could be updated by `SYMBOL_MANAGER_ROLE`:
```solidity
    function setSymbolTradingFee(
        uint256 symbolId,
        uint256 tradingFee
    ) external onlyRole(LibAccessibility.SYMBOL_MANAGER_ROLE) {
        SymbolStorage.Layout storage symbolLayout = SymbolStorage.layout();
        require(symbolId >= 1 && symbolId <= symbolLayout.lastId, "ControlFacet: Invalid id");
        emit SetSymbolTradingFee(symbolId, symbolLayout.symbols[symbolId].tradingFee, tradingFee);
        symbolLayout.symbols[symbolId].tradingFee = tradingFee;
    }
```
This is problematic as an attacker can drain funds from protocol if the `tradingFee` is updated with higher value:

1. Attacker front-runs `setSymbolTradingFee` transaction and [sends a quote](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacet.sol#L13-L59) with **deadline == block.timestamp**, and is charged trading fee
2. `setSymbolTradingFee` transaction gets executed, `tradingFee` is updated to a higher rate
3. Attacker sumbits a transaction to [expire the pending quote](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyA/PartyAFacet.sol#L61-L67) in the next block, as `tradingFee` is higher, attacker is returned with more trading fee than he was charged
4. Attacker deallocates and withdraws, protocol is drained

## Impact

Protocol will be drained of funds.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L122-L133

## Tool used

Manual Review

## Recommendation

The charged trading fee should be recorded per quote, the same amount should be returned when returns trading fee.