xiaoming90

high

# Suspended users can bypass the withdrawal restriction by exploiting `transferAllocation`

## Summary

Suspended users can bypass the withdrawal restriction by exploiting `transferAllocation` function. Thus, an attacker can transfer the ill-gotten gains out of the protocol, leading to a loss of assets for the protocol and its users.

## Vulnerability Detail

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L26

```solidity
File: AccountFacet.sol
26:     function withdraw(uint256 amount) external whenNotAccountingPaused notSuspended(msg.sender) {
27:         AccountFacetImpl.withdraw(msg.sender, amount);
28:         emit Withdraw(msg.sender, msg.sender, amount);
29:     }
30: 
31:     function withdrawTo(
32:         address user,
33:         uint256 amount
34:     ) external whenNotAccountingPaused notSuspended(msg.sender) {
35:         AccountFacetImpl.withdraw(user, amount);
36:         emit Withdraw(msg.sender, user, amount);
37:     }
```

When a user is suspended, they are not allowed to call any of the withdraw functions (`withdraw` and `withdrawTo`) to withdraw funds from their account. These withdrawal functions are guarded by the `notSuspended` modifier that will revert if the user's address is suspended.

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/utils/Accessibility.sol#L73

```solidity
File: Accessibility.sol
73:     modifier notSuspended(address user) {
74:         require(
75:             !AccountStorage.layout().suspendedAddresses[user],
76:             "Accessibility: Sender is Suspended"
77:         );
78:         _;
79:     }
```

However, suspected users can bypass this restriction by exploiting the `transferAllocation` function to transfer the assets from a suspended account to another non-suspended account and withdraw the assets from there.

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L98

```solidity
function transferAllocation(
    uint256 amount,
    address origin,
    address recipient,
    SingleUpnlSig memory upnlSig
) external whenNotPartyBActionsPaused {
    AccountFacetImpl.transferAllocation(amount, origin, recipient, upnlSig);
    emit TransferAllocation(amount, origin, recipient);
}
```

## Impact

In the event of an attack, the protocol will suspend the malicious account and prevent it from transferring ill-gotten gains out of the protocol. However, since this restriction can be bypassed, the attacker can transfer the ill-gotten gains out of the protocol, leading to a loss of assets for the protocol and its users.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L98

## Tool used

Manual Review

## Recommendation

Add the `notSuspended` modifier to the `transferAllocation` function to prevent suspended users from using it to transfer assets.

```diff
function transferAllocation(
    uint256 amount,
    address origin,
    address recipient,
    SingleUpnlSig memory upnlSig
- ) external whenNotPartyBActionsPaused {
+ ) external whenNotPartyBActionsPaused notSuspended(msg.sender) {
    AccountFacetImpl.transferAllocation(amount, origin, recipient, upnlSig);
    emit TransferAllocation(amount, origin, recipient);
}
```