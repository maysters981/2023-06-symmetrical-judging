Juntao

high

# PartyA can avoid being liquidated by increasing partyANonces

## Summary

By by simply increasing `partyANonces`, PartyA can avoid being liquidated.

## Vulnerability Detail

To liquidate a PartyA, the liquidator should first set the user liquidated through [liquidatePartyA(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32) method:
```solidity
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
        MAStorage.Layout storage maLayout = MAStorage.layout();

        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
        require(availableBalance < 0, "LiquidationFacet: PartyA is solvent");
        maLayout.liquidationStatus[partyA] = true;
        maLayout.liquidationTimestamp[partyA] = upnlSig.timestamp;
        AccountStorage.layout().liquidators[partyA].push(msg.sender);
    }
```
This method will [verify](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L87-L106) `SingleUpnlSig` signature before continuing:
```solidity
        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
```
```solidity
    function verifyPartyAUpnl(SingleUpnlSig memory upnlSig, address partyA) internal view {
        MuonStorage.Layout storage muonLayout = MuonStorage.layout();
//        require(
//            block.timestamp <= upnlSig.timestamp + muonLayout.upnlValidTime,
//            "LibMuon: Expired signature"
//        );
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                AccountStorage.layout().partyANonces[partyA],
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
        verifyTSSAndGateway(hash, upnlSig.sigs, upnlSig.gatewaySignature);
    }
```
During the verification process, a hash is generated based on the provided parameters, including `partyANonces`:
```solidity
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                AccountStorage.layout().partyANonces[partyA],
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
```
Symmetrical protocol incorporates **Nonce** as a safeguard, with a nonce in place, each signature becomes single-use, any action that increase the nonce will render the previous signature invalid. 

Based on this, a PartyA can simply front-run and call [allocate(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L39-L44) method in AccountFacet.sol, which will in turn call [allocate(...)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L41-L52) method in AccountFacetImpl.sol:
```solidity
    function allocate(uint256 amount) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        require(
            accountLayout.allocatedBalances[msg.sender] + amount <=
            GlobalAppStorage.layout().balanceLimitPerUser,
            "AccountFacet: Allocated balance limit reached"
        );
        require(accountLayout.balances[msg.sender] >= amount, "AccountFacet: Insufficient balance");
        accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
    }
```
As we can see from the code, `partyANonces` will be increased in the method:
```solidity
        accountLayout.partyANonces[msg.sender] += 1;
``` 
By allocating just **1 wei**, PartyA can increase the `partyANonces` and liquidator's transaction will then be reverted due to a failed signature verification.

## Impact

PartyA can avoid being liquidated.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L87-L106

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacetImpl.sol#L41-L52

## Tool used

Manual Review

## Recommendation

No need to increase `partyANonces` when allocates:
```diff
    function allocate(uint256 amount) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        require(
            accountLayout.allocatedBalances[msg.sender] + amount <=
            GlobalAppStorage.layout().balanceLimitPerUser,
            "AccountFacet: Allocated balance limit reached"
        );
        require(accountLayout.balances[msg.sender] >= amount, "AccountFacet: Insufficient balance");
-       accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
    }
```