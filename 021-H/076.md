ver0759

high

# Some functions use partyANonces and partyBNonces for signature verification but do not increment the nonce

## Summary
Some functions internally call the verify function in `LibMuon.sol` for verification, which internally use `partyANonces` and `partyBNonces` for signature verification, but these do not self-increment `partyANonces` and `partyBNonces`, resulting in possible signature reuse.

## Vulnerability Detail
Some functions internally call the verify function in `LibMuon.sol` for verification, for example `liquidatePartyA`:
```solidity
LiquidationFacet.sol
    function liquidatePartyA(
        address partyA,
        SingleUpnlSig memory upnlSig
    )
    external
    whenNotLiquidationPaused
    notLiquidatedPartyA(partyA)
    onlyRole(LibAccessibility.LIQUIDATOR_ROLE)
    {
        LiquidationFacetImpl.liquidatePartyA(partyA, upnlSig);  // <- here
        emit LiquidatePartyA(msg.sender, partyA);
    }

LiquidationFacetImpl.sol
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
        MAStorage.Layout storage maLayout = MAStorage.layout();

        LibMuon.verifyPartyAUpnl(upnlSig, partyA); // <- here
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
        require(availableBalance < 0, "LiquidationFacet: PartyA is solvent");
        maLayout.liquidationStatus[partyA] = true;
        maLayout.liquidationTimestamp[partyA] = upnlSig.timestamp;
        AccountStorage.layout().liquidators[partyA].push(msg.sender);
    }
```
And `LibMuon.verifyPartyAUpnl` used `partyANonces` to verify the sig:
```solidity
LibMuon.sol
    function verifyPartyAUpnl(SingleUpnlSig memory upnlSig, address partyA) internal view {
        MuonStorage.Layout storage muonLayout = MuonStorage.layout();
//        require(
//            block.timestamp <= upnlSig.timestamp + muonLayout.upnlValidTime,
//            "LibMuon: Expired signature"
//        );
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
                AccountStorage.layout().partyANonces[partyA], // <- here
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
        verifyTSSAndGateway(hash, upnlSig.sigs, upnlSig.gatewaySignature);
    }
```
However, `liquidatePartyA` does not auto-increment partyANonces, resulting in reusable signatures. There are many similar places.


## Impact
Signature reuse

## Code Snippet
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L20-L32
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L34-L97
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L240-L306
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L308-L383

## Tool used
Manual Review

## Recommendation
Add nonce auto-increment logic:

```solidity
LiquidationFacetImpl.sol
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
        MAStorage.Layout storage maLayout = MAStorage.layout();

        LibMuon.verifyPartyAUpnl(upnlSig, partyA);
+       AccountStorage.layout().partyANonces[partyA] += 1;  // <- here
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
        require(availableBalance < 0, "LiquidationFacet: PartyA is solvent");
        maLayout.liquidationStatus[partyA] = true;
        maLayout.liquidationTimestamp[partyA] = upnlSig.timestamp;
        AccountStorage.layout().liquidators[partyA].push(msg.sender);
    }
```
