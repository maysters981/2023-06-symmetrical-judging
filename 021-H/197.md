bin2chen

high

# Liquidation missing partyNonces++

## Summary
The current methods of `LiquidationFacetImpl` are not modified partyANonces/partyBNonces
The `unpl` has changed after the liquidation, but since the Nonces are not modified, the signature `upnlSig` that was still within the validity of `upnlValidTime` can still be used, and the old `unpl` can be used, which will cause multiple error judgments

## Vulnerability Detail
During the liquidation process, the status of Quote is modified, which will affect the unpl of partyA/partyB, but the liquidation method does not modify `Nonces`.
For example: `liquidatePositionsPartyB()` does not modify `Nonces

```solidity
    function liquidatePositionsPartyB(
        address partyB,
        address partyA,
        QuotePriceSig memory priceSig
    ) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        MAStorage.Layout storage maLayout = MAStorage.layout();
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();

        LibMuon.verifyQuotePrices(priceSig);
        require(
            priceSig.timestamp <=
                maLayout.partyBLiquidationTimestamp[partyB][partyA] + maLayout.liquidationTimeout,
            "LiquidationFacet: Expired signature"
        );
        require(
            maLayout.partyBLiquidationStatus[partyB][partyA],
            "LiquidationFacet: PartyB is solvent"
        );
        require(
            block.timestamp <= priceSig.timestamp + maLayout.liquidationTimeout,
            "LiquidationFacet: Expired price sig"
        );
        for (uint256 index = 0; index < priceSig.quoteIds.length; index++) {
            Quote storage quote = quoteLayout.quotes[priceSig.quoteIds[index]];
            require(
                quote.quoteStatus == QuoteStatus.OPENED ||
                    quote.quoteStatus == QuoteStatus.CLOSE_PENDING ||
                    quote.quoteStatus == QuoteStatus.CANCEL_CLOSE_PENDING,
                "LiquidationFacet: Invalid state"
            );
            require(
                quote.partyA == partyA && quote.partyB == partyB,
                "LiquidationFacet: Invalid party"
            );

            quote.quoteStatus = QuoteStatus.LIQUIDATED;
            quote.modifyTimestamp = block.timestamp;

            // accountLayout.allocatedBalances[partyA] += quote.lockedValues.cva;
            accountLayout.lockedBalances[partyA].subQuote(quote);

            // (bool hasMadeProfit, uint256 amount) = LibQuote.getValueOfQuoteForPartyA(
            //     priceSig.prices[index],
            //     LibQuote.quoteOpenAmount(quote),
            //     quote
            // );

            // if (hasMadeProfit) {
            //     accountLayout.allocatedBalances[partyA] += amount;
            // } else {
            //     accountLayout.allocatedBalances[partyA] -= amount;
            // }
            quote.avgClosedPrice =
                (quote.avgClosedPrice *
                    quote.closedAmount +
                    LibQuote.quoteOpenAmount(quote) *
                    priceSig.prices[index]) /
                (quote.closedAmount + LibQuote.quoteOpenAmount(quote));
            quote.closedAmount = quote.quantity;

            LibQuote.removeFromOpenPositions(quote.id);
            quoteLayout.partyAPositionsCount[partyA] -= 1;
            quoteLayout.partyBPositionsCount[partyB][partyA] -= 1;
        }
        if (maLayout.partyBPositionLiquidatorsShare[partyB][partyA] > 0) {
            accountLayout.allocatedBalances[msg.sender] +=
                maLayout.partyBPositionLiquidatorsShare[partyB][partyA] *
                priceSig.quoteIds.length;
        }

        if (quoteLayout.partyBPositionsCount[partyB][partyA] == 0) {
            maLayout.partyBLiquidationStatus[partyB][partyA] = false;
            maLayout.partyBLiquidationTimestamp[partyB][partyA] = 0;
        }
    }
```

This leads to a problem, after the liquidation is over, if the previous signature of `upnlSig` within the validity of `upnlValidTime` can still be reused
For example
1. execute liquidatePartyB(upnlSig) => partyBLiquidationStatus[partyB][partyA] = true
2. liquidatePositionsPartyB() => partyBLiquidationStatus[partyB][partyA] = false
3. in the same transaction, use the same `unnlSig` and then execute maliciously: liquidatePartyB(upnlSig) => partyBLiquidationStatus[partyB][partyA] = true

partyBLiquidationStatus[partyB][partyA] Again marked as in liquidation ï¼ˆIt is possible to be locked forever due to the `liquidationTimeout` limitation)

`liquidatePartyA` has the same problem
Since `unpl` is wrong, there are many places where this value is used that are problematic

For example: `deallocate()` can try to retrieve more funds with the wrong `unpl`
 etc.


## Impact
The old `unpl` can be used again, leading to an error in the logic that requires `unpl`

## Code Snippet
https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L308

## Tool used

Manual Review

## Recommendation

After liquidation execution:

`AccountStorage.layout().partyANonces[quote.partyA]++`
`AccountStorage.layout().partyBNonces[partyB][partyA]++`

