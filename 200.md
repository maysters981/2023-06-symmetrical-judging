Jiamin

high

# User may be returned with more/less trading fee

## Summary

User may be returned with more/less trading fee.

## Vulnerability Detail

Protocol implements 2 functions to handle trading fee, both are in **LibQuote.sol**.

When user opens a quote, function [receiveTradingFee(uint256 quoteId)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L142-L147) is called to charge trading fee from user:
```solidity
    function receiveTradingFee(uint256 quoteId) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
        accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] -= tradingFee;
        accountLayout.balances[GlobalAppStorage.layout().feeCollector] += tradingFee;
    }
```
When user's quote is cancelled or expired, function [returnTradingFee(uint256 quoteId)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L135-L140) is called to return trading fee to user:
```solidity
    function returnTradingFee(uint256 quoteId) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        uint256 tradingFee = LibQuote.getTradingFee(quoteId);
        accountLayout.allocatedBalances[QuoteStorage.layout().quotes[quoteId].partyA] += tradingFee;
        accountLayout.balances[GlobalAppStorage.layout().feeCollector] -= tradingFee;
    }
```
Both of the functions calculate trading fee through function [getTradingFee(uint256 quoteId)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L122-L133):
```solidity
    function getTradingFee(uint256 quoteId) internal view returns (uint256 fee) {
        QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
        Quote storage quote = quoteLayout.quotes[quoteId];
        Symbol storage symbol = SymbolStorage.layout().symbols[quote.symbolId];
        if (quote.orderType == OrderType.LIMIT) {
            fee =
                (LibQuote.quoteOpenAmount(quote) * quote.requestedOpenPrice * symbol.tradingFee) /
                1e36;
        } else {
            fee = (LibQuote.quoteOpenAmount(quote) * quote.marketPrice * symbol.tradingFee) / 1e36;
        }
    }
```
Function **getTradingFee** retrieves [symbol.tradingFee](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/storages/SymbolStorage.sol#L13) from **SymbolStorage.sol**, and **symbol.tradingFee** is the trading fee rate of the [symbol](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/storages/SymbolStorage.sol#L7) of the quote.
Meanwhile, **symbol.tradingFee** can be updated by [SYMBOL_MANAGER_ROLE](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibAccessibility.sol#L12) through function[setSymbolTradingFee(uint256 symbolId, uint256 tradingFee)](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/control/ControlFacet.sol#L164-L172):
```solidity
    function setSymbolTradingFee(
        uint256 symbolId,
        uint256 tradingFee
    ) external onlyRole(LibAccessibility.SYMBOL_MANAGER_ROLE) {
        SymbolStorage.Layout storage symbolLayout = SymbolStorage.layout();
        require(symbolId >= 1 && symbolId <= symbolLayout.lastId, "ControlFacet: Invalid id");
        emit SetSymbolTradingFee(symbolId, symbolLayout.symbols[symbolId].tradingFee, tradingFee);
        symbolLayout.symbols[symbolId].tradingFee = tradingFee;
    }
```
It is worth noting that if **symbol.tradingFee** is updated when a quote is in pending, then when this quote is cancelled, user may be returned with more/less trading fee. 
Imagine the following scenario:

1. Alice sends a quote, quote.quantity is 1000 and trading fee rate is 5%, so she is charged 50 trading fee
2. Alice's quote is in pending and then SYMBOL_MANAGER_ROLE updated trading fee rate to 10%
3. Alice close her quote, protocol calculates returned trading fee based on new rate, so Alice is returned with 100 trading fee, more than what she paid before

If trading fee is updated to less than 5%, Alice will be returned with less trading fee.

## Impact

Trading fee is not returned to user correctly, protocol or user will lose funds.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L142-L147

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibQuote.sol#L135-L140

## Tool used

Manual Review

## Recommendation

The trading fee amount paid by user should be recored in the quote, when quote is cancelled or expired, protocol can returns the same amount of trading fee to user.