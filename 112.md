bin2chen

medium

# liquidatePartyA() Liquidate DOS

## Summary
When `liquidatePartyA()` executes `LibMuon.verifyPartyAUpnl()` internally, `partyANonces[partyA]` will be part of the signature hash
A malicious `partyA` can front-run ,  execute `AccountFacet.allocate(1 wei)` to modify `partyANonces[partyA]` to invalidate the signature, causing `liquidatePartyA()` to fail
thus preventing liquidation at a very low cost

## Vulnerability Detail
`liquidatePartyA()` will call  `LibMuon.verifyPartyAUpnl()` 
```solidity
    function liquidatePartyA(address partyA, SingleUpnlSig memory upnlSig) internal {
...
@>      LibMuon.verifyPartyAUpnl(upnlSig, partyA);
        int256 availableBalance = LibAccount.partyAAvailableBalanceForLiquidation(
            upnlSig.upnl,
            partyA
        );
```

in `verifyPartyAUpnl()` , hash contains `partyANonces[partyA]`
```solidity
    function verifyPartyAUpnl(SingleUpnlSig memory upnlSig, address partyA) internal view {
....
        bytes32 hash = keccak256(
            abi.encodePacked(
                muonLayout.muonAppId,
                upnlSig.reqId,
                address(this),
                partyA,
@>              AccountStorage.layout().partyANonces[partyA],
                upnlSig.upnl,
                upnlSig.timestamp,
                getChainId()
            )
        );
```

A malicious `partyA` can front-run ,  execute `AccountFacet.allocate(1 wei)` to modify `partyANonces[partyA]` to invalidate the signature, causing `liquidatePartyA()` to fail
```solidity
    function allocate(uint256 amount) internal {
        AccountStorage.Layout storage accountLayout = AccountStorage.layout();
        require(
            accountLayout.allocatedBalances[msg.sender] + amount <=
            GlobalAppStorage.layout().balanceLimitPerUser,
            "AccountFacet: Allocated balance limit reached"
        );
        require(accountLayout.balances[msg.sender] >= amount, "AccountFacet: Insufficient balance");
@>      accountLayout.partyANonces[msg.sender] += 1;
        accountLayout.balances[msg.sender] -= amount;
        accountLayout.allocatedBalances[msg.sender] += amount;
    }
```

So malicious users can repeatedly postpone being liquidated by paying a very low cost (GAS and 1Wei)


Note: liquidatePartyB/allocateForPartyB/transferAllocation There are similar problems
## Impact
Forced to prevent being liquidated until the price in their favor

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/liquidation/LiquidationFacetImpl.sol#L23

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/libraries/LibMuon.sol#L99

## Tool used

Manual Review

## Recommendation

add the minimum amount of allocate, thus increasing the cost of DOS
