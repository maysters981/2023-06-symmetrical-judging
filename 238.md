xiaoming90

high

# Vulnerable to replay attack as `lockQuote` did not increment PartyB's nonce

## Summary

The `lockQuote` function did not increment the PartyB's nonce as one of the mapping keys `quote.partyB` is not initialized. As a result, the protocols are vulnerable to replay attacks.

## Vulnerability Detail

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L30

```solidity
File: PartyBFacetImpl.sol
22:     function lockQuote(uint256 quoteId, SingleUpnlSig memory upnlSig, bool increaseNonce) internal {
23:         QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
24:         AccountStorage.Layout storage accountLayout = AccountStorage.layout();
25: 
26:         Quote storage quote = quoteLayout.quotes[quoteId];
27:         LibMuon.verifyPartyBUpnl(upnlSig, msg.sender, quote.partyA);
28:         checkPartyBValidationToLockQuote(quoteId, upnlSig.upnl);
29:         if (increaseNonce) {
30:             accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
31:         }
32:         quote.modifyTimestamp = block.timestamp;
33:         quote.quoteStatus = QuoteStatus.LOCKED;
34:         quote.partyB = msg.sender;
35:         // lock funds for partyB
36:         accountLayout.partyBPendingLockedBalances[msg.sender][quote.partyA].addQuote(quote);
37:         quoteLayout.partyBPendingQuotes[msg.sender][quote.partyA].push(quote.id);
38:     }
```

In Line 30, it attempts to increment the nonce of `partyBNonces[PartyB's address][PartyA's address]` if `increaseNonce` is true.

However, `quote.partyB` has not been initialized yet and will always be `address(0)`. Thus, instead of incrementing the nonce of `partyBNonces[PartyB's address][PartyA's address]`, the code increment the nonce of `partyBNonces[0][PartyA's address]`

The `quote.partyB` will only be initialized later at Line 34. 

In summary, the nonce of PartyB is not incremented within the `lockQuote` function.

## Impact

The `lockQuote` and `allocateAndLockQuote` functions will be vulnerable to replay attacks as PartyB's nonce is not incremented after executing the function. The same unrealized profit and loss signature (`upnlSig`) can be re-used on the `lockQuote` and `allocateAndLockQuote` functions, or other functions that accept an unrealized profit and loss signature (`upnlSig`) without requesting/generating a new `upnlSig` from Muon.

For instance, if the new/latest PartyB's PnL is worst than the old one, they can choose to re-use and present the old one with better PnL to the protocol, causing their account's PnL to be higher than expected. This indirectly means that PartyB's available balance will be inflated, and they could lock and open more positions than what is allowed if the bug does not exist in the first place. PartyB's assets will be "overutilized," posing a risk to all its corresponding PartyAs. 

This could potentially lead to a loss of assets for PartyA during the liquidation of PartyB. PartyA will receive fewer assets because PartyB's positions are "backed" by fewer assets than expected.

## Code Snippet

https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/PartyB/PartyBFacetImpl.sol#L30

## Tool used

Manual Review

## Recommendation

Increment PartyB's nonce after `quote.partyB` has been initialized.

```diff
function lockQuote(uint256 quoteId, SingleUpnlSig memory upnlSig, bool increaseNonce) internal {
    QuoteStorage.Layout storage quoteLayout = QuoteStorage.layout();
    AccountStorage.Layout storage accountLayout = AccountStorage.layout();

    Quote storage quote = quoteLayout.quotes[quoteId];
    LibMuon.verifyPartyBUpnl(upnlSig, msg.sender, quote.partyA);
    checkPartyBValidationToLockQuote(quoteId, upnlSig.upnl);
-    if (increaseNonce) {
-        accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
-    }
    quote.modifyTimestamp = block.timestamp;
    quote.quoteStatus = QuoteStatus.LOCKED;
    quote.partyB = msg.sender;
    
+    if (increaseNonce) {
+        accountLayout.partyBNonces[quote.partyB][quote.partyA] += 1;
+    }
    
    // lock funds for partyB
    accountLayout.partyBPendingLockedBalances[msg.sender][quote.partyA].addQuote(quote);
    quoteLayout.partyBPendingQuotes[msg.sender][quote.partyA].push(quote.id);
}
```